pragma ValueTypeBehavior: Assertable
import QtQuick
import QtQuick.Layouts
import QtQuick.Shapes
import Qcm.Material as MD
import waywallen.ui as W

MD.Page {
    id: root

    title: 'Displays'
    showHeader: true
    showBackground: false
    readonly property real displayGapPx: 80

    property var selectedId: null

    // FillMode/Rotation enum values mirror proto::FillMode /
    // proto::Rotation (control.proto). Keep the *_VALUES
    // arrays in lockstep with the enum order; *_LABELS is what the UI
    // shows.
    readonly property var kFillModeValues: [1 // STRETCHED
        , 2 // PRESERVE_ASPECT_FIT
        , 3 // PRESERVE_ASPECT_CROP
        , 7  // CENTERED
    ]
    readonly property var kFillModeLabels: ["Stretch", "Fit (preserve aspect)", "Crop (preserve aspect)", "Center (1:1)"]
    function fillmodeIndex(value) {
        const i = root.kFillModeValues.indexOf(value);
        return i < 0 ? 0 : i;
    }

    // Rotation segmented values, mirror proto::Rotation:
    //   1=NORMAL, 2=CW_90, 3=CW_180, 4=CW_270
    readonly property var kRotationValues: [1, 2, 3, 4]
    readonly property var kRotationLabels: ["0°", "90°", "180°", "270°"]
    function rotationIndex(value) {
        const i = root.kRotationValues.indexOf(value);
        return i < 0 ? 0 : i;
    }

    function clampPercent(value) {
        return Math.max(0, Math.min(100, Math.round(Number(value) || 0)));
    }

    function applyLocation(x, y) {
        if (!root.selected)
            return;
        layoutSetQuery.name = root.selected.name;
        layoutSetQuery.displayId = root.selected.id;
        layoutSetQuery.fillmodeSet = false;
        layoutSetQuery.locationSet = true;
        layoutSetQuery.locationX = root.clampPercent(x);
        layoutSetQuery.locationY = root.clampPercent(y);
        layoutSetQuery.alignSet = false;
        layoutSetQuery.rotationSet = false;
        layoutSetQuery.clearFillmode = false;
        layoutSetQuery.clearLocation = false;
        layoutSetQuery.clearAlign = false;
        layoutSetQuery.clearRotation = false;
        layoutSetQuery.reload();
    }

    W.DisplayLayoutSetQuery {
        id: layoutSetQuery
    }

    W.DisplayRenameQuery {
        id: renameQuery
    }

    function layoutRects() {
        const out = [];
        let x = 0;
        for (const d of W.App.displayManager.displays || []) {
            out.push({
                x: x,
                y: 0,
                w: d.width,
                h: d.height,
                d: d
            });
            x += d.width + root.displayGapPx;
        }
        return out;
    }

    readonly property var rects: layoutRects()

    readonly property real boundsW: {
        let max = 0;
        for (const r of rects)
            max = Math.max(max, r.x + r.w);
        return max || 1;
    }
    readonly property real boundsH: {
        let max = 0;
        for (const r of rects)
            max = Math.max(max, r.y + r.h);
        return max || 1;
    }

    function selectedDisplay() {
        if (root.selectedId === null)
            return null;
        for (const d of W.App.displayManager.displays || []) {
            if (d.id === root.selectedId)
                return d;
        }
        return null;
    }

    readonly property var selected: selectedDisplay()

    ColumnLayout {
        anchors.fill: parent
        anchors.leftMargin: 12
        anchors.rightMargin: 12
        spacing: 16

        MD.Pane {
            id: displaysPane
            Layout.fillWidth: true
            Layout.fillHeight: true
            leftPadding: 16
            rightPadding: 16
            radius: 16
            backgroundColor: MD.MProp.color.surface

            contentItem: Item {
                id: canvas
                implicitHeight: 48

                readonly property real padding: 24
                readonly property real viewScale: {
                    const availW = Math.max(1, width - padding * 2);
                    const availH = Math.max(1, height - padding * 2);
                    return Math.min(availW / root.boundsW, availH / root.boundsH);
                }
                readonly property real offsetX: (width - root.boundsW * viewScale) / 2
                readonly property real offsetY: (height - root.boundsH * viewScale) / 2

                MouseArea {
                    anchors.fill: parent
                    onClicked: root.selectedId = null
                }

                ColumnLayout {
                    anchors.centerIn: parent
                    width: Math.min(parent.width - 64, 480)
                    spacing: 12
                    visible: (root.rects.length === 0)

                    MD.Text {
                        Layout.alignment: Qt.AlignHCenter
                        text: qsTr("No displays registered")
                        typescale: MD.Token.typescale.title_medium
                        color: MD.Token.color.on_surface_variant
                    }

                    // KDE-specific install hint. Self-gated on
                    // `W.Util.desktop`; on other DEs (wlroots, niri,
                    // …) the daemon spawns its own layer-shell
                    // backend so this collapses to nothing.
                    W.KdeDisplaysHelp {
                        Layout.fillWidth: true
                    }
                }

                Repeater {
                    model: root.rects

                    delegate: Item {
                        id: rectItem
                        required property int index
                        required property var modelData

                        readonly property var d: modelData.d
                        readonly property bool hasLink: (d.links && d.links.length > 0)
                        readonly property bool isSelected: (root.selectedId === d.id)

                        x: canvas.offsetX + modelData.x * canvas.viewScale
                        y: canvas.offsetY + modelData.y * canvas.viewScale
                        width: modelData.w * canvas.viewScale
                        height: modelData.h * canvas.viewScale

                        Shape {
                            anchors.fill: parent
                            preferredRendererType: Shape.CurveRenderer
                            antialiasing: true

                            ShapePath {
                                strokeColor: rectItem.isSelected ? MD.Token.color.primary : MD.Token.color.outline
                                strokeWidth: rectItem.isSelected ? 3 : 1.5
                                fillColor: rectItem.hasLink ? MD.Token.color.primary_container : MD.Token.color.surface_container_highest
                                capStyle: ShapePath.RoundCap
                                joinStyle: ShapePath.RoundJoin

                                PathRectangle {
                                    x: 0
                                    y: 0
                                    width: rectItem.width
                                    height: rectItem.height
                                    radius: 10
                                }
                            }
                        }

                        MouseArea {
                            anchors.fill: parent
                            onClicked: root.selectedId = rectItem.d.id
                        }

                        ColumnLayout {
                            anchors.centerIn: parent
                            width: Math.max(0, rectItem.width - 12)
                            spacing: 4

                            MD.Text {
                                Layout.fillWidth: true
                                text: rectItem.d.displayLabel || rectItem.d.name || ("Display " + rectItem.d.id)
                                typescale: MD.Token.typescale.title_small
                                color: rectItem.hasLink ? MD.Token.color.on_primary_container : MD.Token.color.on_surface
                                horizontalAlignment: Text.AlignHCenter
                                elide: Text.ElideMiddle
                            }

                            MD.Text {
                                Layout.alignment: Qt.AlignHCenter
                                text: rectItem.d.width + " × " + rectItem.d.height
                                typescale: MD.Token.typescale.label_medium
                                color: rectItem.hasLink ? MD.Token.color.on_primary_container : MD.Token.color.on_surface_variant
                            }
                        }

                        MD.Text {
                            anchors.left: parent.left
                            anchors.top: parent.top
                            anchors.margins: 6
                            text: "#" + rectItem.d.id
                            typescale: MD.Token.typescale.label_small
                            color: rectItem.hasLink ? MD.Token.color.on_primary_container : MD.Token.color.on_surface_variant
                        }

                        W.GpuTag {
                            anchors.right: parent.right
                            anchors.top: parent.top
                            anchors.margins: 6
                            drmRenderMajor: rectItem.d.drmRenderMajor || 0
                            drmRenderMinor: rectItem.d.drmRenderMinor || 0
                        }
                    }
                }
            }
        }

        // --- Inline details panel (squeezes out below canvas) ---
        MD.Pane {
            id: detailsPane
            Layout.fillWidth: true
            Layout.preferredHeight: root.selected ? implicitHeight : 0

            leftPadding: 16
            rightPadding: 16
            bottomPadding: 12

            radius: 16
            backgroundColor: MD.MProp.color.surface
            visible: Layout.preferredHeight > 0.5
            clip: true

            Behavior on Layout.preferredHeight {
                NumberAnimation {
                    duration: 200
                    easing.type: Easing.InOutCubic
                }
            }

            contentItem: ColumnLayout {
                id: detailsContent
                spacing: 8

                RowLayout {
                    Layout.fillWidth: true
                    spacing: 8

                    readonly property bool canRename: W.Util.supportsDisplayRename

                    MD.TextField {
                        id: aliasField
                        Layout.fillWidth: true
                        visible: parent.canRename
                        placeholderText: root.selected ? (root.selected.name || ("Display " + root.selected.id)) : ""
                        readonly property string serverAlias: root.selected ? (root.selected.alias || "") : ""
                        onServerAliasChanged: if (!activeFocus) text = serverAlias
                        Component.onCompleted: text = serverAlias
                        Connections {
                            target: root
                            function onSelectedIdChanged() {
                                aliasField.text = aliasField.serverAlias;
                            }
                        }
                        function commit() {
                            if (!root.selected)
                                return;
                            const trimmed = text.trim();
                            if (trimmed === serverAlias)
                                return;
                            renameQuery.name = root.selected.name;
                            renameQuery.displayId = root.selected.id;
                            renameQuery.alias = trimmed;
                            renameQuery.clear = (trimmed.length === 0);
                            renameQuery.reload();
                        }
                        onAccepted: commit()
                        onActiveFocusChanged: if (!activeFocus) commit()
                    }

                    MD.Text {
                        Layout.fillWidth: true
                        visible: !parent.canRename
                        text: root.selected ? (root.selected.displayLabel || root.selected.name || ("Display " + root.selected.id)) : ""
                        typescale: MD.Token.typescale.title_medium
                        color: MD.Token.color.on_surface
                        elide: Text.ElideRight
                    }

                    MD.IconButton {
                        visible: parent.canRename && !!root.selected && (root.selected.alias || "").length > 0
                        icon.name: MD.Token.icon.refresh
                        MD.ToolTip {
                            visible: parent.hovered
                            text: "Reset to compositor name"
                        }
                        onClicked: {
                            if (!root.selected)
                                return;
                            renameQuery.name = root.selected.name;
                            renameQuery.displayId = root.selected.id;
                            renameQuery.alias = "";
                            renameQuery.clear = true;
                            renameQuery.reload();
                            aliasField.text = "";
                        }
                    }

                    MD.IconButton {
                        icon.name: MD.Token.icon.close
                        onClicked: root.selectedId = null
                    }
                }

                RowLayout {
                    Layout.fillWidth: true
                    spacing: 24

                    RowLayout {
                        spacing: 8
                        MD.Text {
                            text: "ID:"
                            typescale: MD.Token.typescale.label_medium
                            color: MD.Token.color.on_surface_variant
                        }
                        MD.Text {
                            text: root.selected ? "#" + root.selected.id : ""
                            typescale: MD.Token.typescale.body_medium
                            color: MD.Token.color.on_surface
                        }
                    }

                    RowLayout {
                        spacing: 8
                        MD.Text {
                            text: "Size:"
                            typescale: MD.Token.typescale.label_medium
                            color: MD.Token.color.on_surface_variant
                        }
                        MD.Text {
                            text: root.selected ? root.selected.width + " × " + root.selected.height : ""
                            typescale: MD.Token.typescale.body_medium
                            color: MD.Token.color.on_surface
                        }
                    }

                    RowLayout {
                        visible: !!root.selected && root.selected.refreshMhz > 0
                        spacing: 8
                        MD.Text {
                            text: "Refresh:"
                            typescale: MD.Token.typescale.label_medium
                            color: MD.Token.color.on_surface_variant
                        }
                        MD.Text {
                            text: root.selected ? (root.selected.refreshMhz / 1000).toFixed(3) + " Hz" : ""
                            typescale: MD.Token.typescale.body_medium
                            color: MD.Token.color.on_surface
                        }
                    }

                    Item {
                        Layout.fillWidth: true
                    }
                }

                MD.Divider {
                    Layout.fillWidth: true
                    Layout.topMargin: 4
                    Layout.bottomMargin: 4
                }

                MD.Text {
                    text: "Connected renderer"
                    typescale: MD.Token.typescale.title_small
                    color: MD.Token.color.on_surface
                }

                RowLayout {
                    id: connectedRendererRow
                    readonly property string connectedId: {
                        if (!root.selected)
                            return "";
                        const links = root.selected.links || [];
                        return links.length > 0 ? (links[0].rendererId || "") : "";
                    }
                    // Re-resolve when the manager's renderer list changes
                    // (the `renderers` access wires up the dependency) so a
                    // late RendererUpsert or a RendererRemoved is reflected
                    // without manual refresh.
                    readonly property var renderer: {
                        const _ = W.App.rendererManager.renderers;
                        return connectedId.length > 0 ? W.App.rendererManager.get(connectedId) : null;
                    }
                    Layout.fillWidth: true
                    spacing: 8

                    MD.Icon {
                        readonly property string status: connectedRendererRow.renderer ? connectedRendererRow.renderer.status : ""
                        name: {
                            if (!connectedRendererRow.renderer)
                                return MD.Token.icon.pause;
                            return status === "paused" ? MD.Token.icon.pause : MD.Token.icon.play_arrow;
                        }
                        size: 24
                        color: !connectedRendererRow.renderer || status === "paused" ? MD.Token.color.on_surface_variant : MD.Token.color.primary
                    }

                    ColumnLayout {
                        Layout.fillWidth: true
                        spacing: 0

                        MD.Text {
                            Layout.fillWidth: true
                            text: {
                                const r = connectedRendererRow.renderer;
                                if (r) {
                                    const name = (r.name && r.name.length) ? r.name : "renderer";
                                    return r.pid > 0 ? (name + "-" + r.pid) : name;
                                }
                                if (connectedRendererRow.connectedId.length > 0) {
                                    return connectedRendererRow.connectedId;
                                }
                                return "Idle — no renderer connected.";
                            }
                            typescale: MD.Token.typescale.body_medium
                            color: connectedRendererRow.renderer ? MD.Token.color.on_surface : MD.Token.color.on_surface_variant
                            font.family: connectedRendererRow.renderer ? "monospace" : ""
                            elide: Text.ElideMiddle
                        }

                        MD.Text {
                            Layout.fillWidth: true
                            visible: !!connectedRendererRow.renderer
                            text: {
                                const r = connectedRendererRow.renderer;
                                if (!r)
                                    return "";
                                return (r.status || "") + " · " + (r.fps || 0) + " fps";
                            }
                            typescale: MD.Token.typescale.label_small
                            color: MD.Token.color.on_surface_variant
                            elide: Text.ElideRight
                        }
                    }
                }

                // ---- Layout (fillmode + location) ----
                MD.Divider {
                    Layout.fillWidth: true
                    Layout.topMargin: 8
                    Layout.bottomMargin: 4
                    visible: !!root.selected
                }

                RowLayout {
                    Layout.fillWidth: true
                    visible: !!root.selected
                    spacing: 8

                    MD.Text {
                        Layout.fillWidth: true
                        text: "Layout"
                        typescale: MD.Token.typescale.title_small
                        color: MD.Token.color.on_surface
                    }

                    Item {
                        implicitWidth: children[0].implicitWidth
                        MD.IconButton {
                            anchors.verticalCenter: parent.verticalCenter
                            mdState.size: MD.Enum.XS
                            visible: {
                                if (!root.selected)
                                    return false;
                                const ovr = root.selected.layoutOverride || ({});
                                return ovr.fillmodeSet === true || ovr.locationSet === true || ovr.alignSet === true;
                            }
                            icon.name: MD.Token.icon.refresh
                            MD.ToolTip {
                                visible: parent.hovered
                                text: "Revert to global default"
                            }
                            onClicked: {
                                if (!root.selected)
                                    return;
                                layoutSetQuery.name = root.selected.name;
                                layoutSetQuery.displayId = root.selected.id;
                                layoutSetQuery.fillmodeSet = false;
                                layoutSetQuery.locationSet = false;
                                layoutSetQuery.alignSet = false;
                                layoutSetQuery.clearFillmode = true;
                                layoutSetQuery.clearLocation = true;
                                layoutSetQuery.clearAlign = true;
                                layoutSetQuery.clearRotation = false;
                                layoutSetQuery.reload();
                            }
                        }
                    }
                }

                RowLayout {
                    Layout.fillWidth: true
                    visible: !!root.selected
                    spacing: 12

                    ColumnLayout {
                        id: locationGroup
                        Layout.fillWidth: true
                        spacing: 4

                        MD.Text {
                            text: "Fill mode"
                            typescale: MD.Token.typescale.label_medium
                            color: MD.Token.color.on_surface_variant
                        }

                        MD.ComboBox {
                            id: fillmodeBox
                            Layout.fillWidth: true
                            model: root.kFillModeLabels
                            currentIndex: {
                                if (!root.selected)
                                    return 0;
                                const eff = root.selected.effectiveLayout || ({});
                                return root.fillmodeIndex(eff.fillmode || 0);
                            }
                            onActivated: idx => {
                                if (!root.selected)
                                    return;
                                layoutSetQuery.name = root.selected.name;
                                layoutSetQuery.displayId = root.selected.id;
                                layoutSetQuery.fillmodeSet = true;
                                layoutSetQuery.fillmode = root.kFillModeValues[idx];
                                layoutSetQuery.locationSet = false;
                                layoutSetQuery.alignSet = false;
                                layoutSetQuery.rotationSet = false;
                                layoutSetQuery.clearFillmode = false;
                                layoutSetQuery.clearLocation = false;
                                layoutSetQuery.clearAlign = false;
                                layoutSetQuery.clearRotation = false;
                                layoutSetQuery.reload();
                            }
                        }
                    }

                    ColumnLayout {
                        Layout.fillWidth: true
                        spacing: 4

                        readonly property var effective: root.selected ? (root.selected.effectiveLayout || ({})) : ({})
                        readonly property int currentX: root.clampPercent(effective.locationX ?? 50)
                        readonly property int currentY: root.clampPercent(effective.locationY ?? 50)

                        enabled: {
                            if (!root.selected)
                                return false;
                            const eff = root.selected.effectiveLayout || ({});
                            return (eff.fillmode || 0) !== 1;
                        }
                        opacity: enabled ? 1.0 : 0.4

                        RowLayout {
                            Layout.fillWidth: true
                            spacing: 8

                            MD.Text {
                                Layout.preferredWidth: 72
                                text: "Horizontal"
                                typescale: MD.Token.typescale.label_medium
                                color: MD.Token.color.on_surface_variant
                            }

                            MD.Slider {
                                id: horizontalLocation
                                Layout.fillWidth: true
                                from: 0
                                to: 100
                                stepSize: 1
                                value: locationGroup.currentX
                                onMoved: root.applyLocation(value, verticalLocation.value)
                            }

                            MD.Text {
                                Layout.preferredWidth: 44
                                text: qsTr("%1%").arg(root.clampPercent(horizontalLocation.value))
                                typescale: MD.Token.typescale.label_medium
                                color: MD.Token.color.on_surface_variant
                                horizontalAlignment: Text.AlignRight
                            }
                        }

                        RowLayout {
                            Layout.fillWidth: true
                            spacing: 8

                            MD.Text {
                                Layout.preferredWidth: 72
                                text: "Vertical"
                                typescale: MD.Token.typescale.label_medium
                                color: MD.Token.color.on_surface_variant
                            }

                            MD.Slider {
                                id: verticalLocation
                                Layout.fillWidth: true
                                from: 0
                                to: 100
                                stepSize: 1
                                value: locationGroup.currentY
                                onMoved: root.applyLocation(horizontalLocation.value, value)
                            }

                            MD.Text {
                                Layout.preferredWidth: 44
                                text: qsTr("%1%").arg(root.clampPercent(verticalLocation.value))
                                typescale: MD.Token.typescale.label_medium
                                color: MD.Token.color.on_surface_variant
                                horizontalAlignment: Text.AlignRight
                            }
                        }
                    }

                    ColumnLayout {
                        spacing: 4

                        MD.Text {
                            text: "Rotation"
                            typescale: MD.Token.typescale.label_medium
                            color: MD.Token.color.on_surface_variant
                        }

                        MD.SegmentedButtonGroup {
                            id: rotationGroup
                            size: MD.Enum.XS

                            // Inline buttons; SegmentedButtonGroup's
                            // updatePositions only recognises segmented
                            // buttons that are direct children — a
                            // Repeater here ends up in contentModel as
                            // an extra slot and shifts PosFirst off the
                            // real first button.
                            function applyRotation(rotationValue) {
                                if (!root.selected)
                                    return;
                                layoutSetQuery.name = root.selected.name;
                                layoutSetQuery.displayId = root.selected.id;
                                layoutSetQuery.fillmodeSet = false;
                                layoutSetQuery.locationSet = false;
                                layoutSetQuery.alignSet = false;
                                layoutSetQuery.rotationSet = true;
                                layoutSetQuery.rotation = rotationValue;
                                layoutSetQuery.clearFillmode = false;
                                layoutSetQuery.clearLocation = false;
                                layoutSetQuery.clearAlign = false;
                                layoutSetQuery.clearRotation = false;
                                layoutSetQuery.reload();
                            }
                            function isChecked(rotationValue) {
                                if (!root.selected)
                                    return rotationValue === 1; // ROTATION_NORMAL
                                const eff = root.selected.effectiveLayout || ({});
                                return (eff.rotation || 0) === rotationValue;
                            }

                            MD.SegmentedButton {
                                text: root.kRotationLabels[0]
                                checked: rotationGroup.isChecked(root.kRotationValues[0])
                                onClicked: rotationGroup.applyRotation(root.kRotationValues[0])
                            }
                            MD.SegmentedButton {
                                text: root.kRotationLabels[1]
                                checked: rotationGroup.isChecked(root.kRotationValues[1])
                                onClicked: rotationGroup.applyRotation(root.kRotationValues[1])
                            }
                            MD.SegmentedButton {
                                text: root.kRotationLabels[2]
                                checked: rotationGroup.isChecked(root.kRotationValues[2])
                                onClicked: rotationGroup.applyRotation(root.kRotationValues[2])
                            }
                            MD.SegmentedButton {
                                text: root.kRotationLabels[3]
                                checked: rotationGroup.isChecked(root.kRotationValues[3])
                                onClicked: rotationGroup.applyRotation(root.kRotationValues[3])
                            }
                        }
                    }

                    Item {
                        Layout.fillWidth: true
                    }
                }
            }
        }
        Item {}
    }
}
